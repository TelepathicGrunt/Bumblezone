import net.fabricmc.loom.task.RemapJarTask

buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
	}
}

plugins {
	id "net.darkhax.curseforgegradle" version "1.0.7"
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = "${project.mod_version}+${project.minecraft_version}"
archivesBaseName = "${project.modid}_fabric"
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/the_bumblezone.accesswidener")
}

repositories {
	maven {
		url = 'https://maven.shedaniel.me'
		content {
			includeGroup 'me.shedaniel.cloth'
			includeGroup 'me.shedaniel'
		}
	}
	maven {
		url = 'https://www.cursemaven.com'
		content {
			includeGroup 'curse.maven'
		}
	}
	maven {
		url = 'https://api.modrinth.com/maven/'
		content {
			includeGroup 'maven.modrinth'
		}
	}
	maven {
		url = 'https://maven.cafeteria.dev/releases'
		content {
			includeGroup 'dev.cafeteria'
		}
	}
	maven {
		name = 'Ladysnake Mods'
		url = 'https://ladysnake.jfrog.io/artifactory/mods'
		content {
			includeGroup 'io.github.ladysnake'
			includeGroupByRegex 'dev\\.onyxstudios.*'
		}
	}
	maven {
		url = 'https://nexus.resourcefulbees.com/repository/maven-public/'
		content {
			includeGroup 'com.telepathicgrunt'
		}
	}
	maven {
		url = 'https://maven.blamejared.com'
		content {
			includeGroup 'mezz.jei'
		}
	}
	maven {
		// location of a maven mirror for JEI files, as a fallback
		name = "ModMaven"
		url = "https://modmaven.dev"
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
		content {
			includeGroupByRegex 'dev\\.emi.*'
		}
	}
	maven {
		url "https://maven.jamieswhiteshirt.com/libs-release"
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}
	maven {
		url = 'https://maven.architectury.dev/'
		content {
			includeGroup 'dev.architectury'
		}
	}
	maven { url 'https://jitpack.io' }
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	annotationProcessor("com.github.LlamaLad7:MixinExtras:${project.mixin_extra_version}")
	modImplementation ("com.github.LlamaLad7:MixinExtras:${project.mixin_extra_version}")
	include ("com.github.LlamaLad7:MixinExtras:${project.mixin_extra_version}")

	modImplementation ("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") {
		force = true //Force the version we want rather than trusting Gradle to pick it over transitive suggestions
	}

	modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}"
	modRuntimeOnly "maven.modrinth:midnightlib:${project.midnightlib_version}"

	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_version}"
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cardinal_version}"
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_version}"
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cardinal_version}"

	// https://github.com/CafeteriaGuild/fake-player-api
	modImplementation "dev.cafeteria:fake-player-api:${project.fake_player_version}"
	include "dev.cafeteria:fake-player-api:${project.fake_player_version}"

	modImplementation ("maven.modrinth:modmenu:${project.mod_menu_version}") { transitive = false }

	modCompileOnly "dev.emi:emi:${project.emi_version}"
	//modRuntimeOnly "dev.emi:emi:${project.emi_version}"

	modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${project.rei_version}"
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
	//modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
	//modRuntimeOnly "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
	//modRuntimeOnly "dev.architectury:architectury-fabric:${project.architectury_version}"

	modCompileOnlyApi "curse.maven:jei-238222:3944625"
	//modCompileOnlyApi "mezz.jei:jei-${project.jeimc_version}-fabric:${project.jei_version}"

	//modRuntimeOnly "com.jamieswhiteshirt:reach-entity-attributes:2.3.0"
	//modRuntimeOnly "curse.maven:travelersbackpacks-541171:4451504"
	//modRuntimeOnly "dev.emi:trinkets:${trinkets_version}"
	modCompileOnly "dev.emi:trinkets:${trinkets_version}"

	//modCompileOnlyApi "mezz.jei:jei-${jei_mc_version}-common-api:${jei_version}"
	//modCompileOnlyApi "mezz.jei:jei-${jei_mc_version}-fabric-api:${jei_version}"
	//modRuntimeOnly "mezz.jei:jei-${jei_mc_version}-common:${jei_version}"
	//modRuntimeOnly "mezz.jei:jei-${jei_mc_version}-fabric:${jei_version}"

	modRuntimeOnly "com.telepathicgrunt:CommandStructures-Fabric:${project.commandstructures_version}"
	modRuntimeOnly "com.telepathicgrunt:StructureVoidToggle-Fabric:${project.structurevoidtoggle_version}"

	modCompileOnly "curse.maven:friends_and_foes-551364:3827720"
	//modCompileOnly "curse.maven:bee_better-351600:3726160"

	//modRuntimeOnly "curse.maven:architectury-419697:3717905"
	//modRuntimeOnly "curse.maven:requiem-265729:3639233"
	//modImplementation ("curse.maven:mobcatcher-498389:3575752") { transitive = false }
	//modCompileOnly ("curse.maven:charm-318872:3355096") { transitive = false }
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}

apply from: 'gradle/maven.gradle'

String changelogText = file("CHANGELOG.md").getText('UTF-8')

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = "$System.env.CURSEFORGEKEY"
	int projectId = 363949

	def mainFile = upload(projectId, remapJar)
	mainFile.changelog = changelogText
	mainFile.changelogType = "markdown"
	mainFile.releaseType = "beta"
	mainFile.displayName = "The Bumblezone v${project.mod_version} (${project.minecraft_version} MC Fabric)"
	mainFile.addRequirement("midnightlib")
	mainFile.addRequirement("fabric-api")
	mainFile.addOptional("continuity")
	mainFile.addOptional("unofficial-sodium-biome-blending-fix")
	mainFile.addOptional("indium")
	mainFile.addIncompatibility("lanterns-bow")
}

modrinth {
	token = System.getenv("MODRINTH")
	versionName = "The Bumblezone"
	versionNumber = project.version + "+fabric"
	projectId = "eA8SXqWL"
	uploadFile = remapJar
	changelog = changelogText
	versionType = "BETA"
	gameVersions = ["1.19.2"]
	loaders = ["fabric"]
	dependencies {
		required.project "qsl"
		required.project "midnightlib"
		optional.project "continuity"
		optional.project "unofficial-sodium-biome-blending-fix"
		optional.project "indium"
		incompatible.project "lanterns-bow"
	}
}

task buildAndRelease{}
tasks.publish.mustRunAfter tasks.build
tasks.publishCurseForge.mustRunAfter tasks.publish
tasks.modrinth.mustRunAfter tasks.publish
tasks.publishCurseForge.dependsOn("remapJar")
tasks.buildAndRelease.dependsOn(tasks.build, tasks.publish, tasks.publishCurseForge, tasks.modrinth)
