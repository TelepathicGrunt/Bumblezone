plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    common("fabric", "quilt", "forge")
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

dependencies {
    // REQUIRED
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    modImplementation "maven.modrinth:midnightlib:${rootProject.midnightlib}"

    modRuntimeOnly(modCompileOnly("earth.terrarium.athena:athena-fabric-${rootProject.athena}"))

    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${rootProject.cardinal_components}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${rootProject.cardinal_components}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }

    implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${rootProject.mixin_extras}"))


    // COMPAT
    modCompileOnly("curse.maven:lootr_fabric-615106:${project.lootr_fabric_file_id}")
    //modRuntimeOnly("curse.maven:lootr_fabric-615106:${project.lootr_fabric_file_id}")

    modCompileOnly "maven.modrinth:modmenu:${rootProject.mod_menu}"
    modRuntimeOnly "maven.modrinth:modmenu:${rootProject.mod_menu}"

    modCompileOnly("dev.emi:trinkets:${trinkets}")

    modCompileOnly "dev.emi:emi-fabric:${rootProject.emi}"

    modCompileOnly "curse.maven:restricted-portals-228266:${rootProject.restricted_portals_file_id}"

    modCompileOnly("maven.modrinth:sodium:${rootProject.sodium}")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set(null)
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}